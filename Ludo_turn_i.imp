/* Ludo_turn_i
 * Author: anaca
 * Creation date: 25/06/2025
 */

IMPLEMENTATION Ludo_turn_i
REFINES Ludo_turn

SEES Ludo_ctx


CONCRETE_VARIABLES
  gameStarted,
  colorEnabled, //concrete
  color,
  hasRoll,
  diceValue,
  sixSequenceCount,
  finishList,
  finishCount

INVARIANT
  finishList : 0..3 --> COLORS &
  finishCount : 0..4 &
  0..finishCount-1 <| finishList = finishOrder &
  finishCount = card(finishOrder) &
  colorEnabled : COLORS --> BOOL &
  dom(colorEnabled |> {TRUE}) = enabledColors

INITIALISATION
  gameStarted := FALSE;
  colorEnabled := COLORS * {FALSE};
  color := colorsOrder(0);
  hasRoll := FALSE;
  diceValue := 0;
  sixSequenceCount := 0;
  finishList := colorsOrder;
  finishCount := 0


LOCAL_OPERATIONS
   bb <-- isColorFinished(cc) =
       PRE cc : COLORS
       THEN
           bb := bool(cc : ran(finishList))
       END;

   nn <-- turnIndex =
       nn := colorsOrder~(color)

OPERATIONS

  bb <-- isColorFinished(cc) =
  BEGIN
      VAR ii, color IN
          ii := 0;
          bb := FALSE;
          WHILE ii < finishCount & bb = FALSE
          DO
            color := finishList(ii);
            bb := bool(color = cc);
            ii := ii + 1
          INVARIANT ii : 0..finishCount & ((bb = TRUE) <=> (cc : finishList[0..ii-1]))
          VARIANT numColors - ii
          END
      END
  END;

  nn <-- turnIndex =
  BEGIN
      VAR ii, maxII, isColorTurn, cc IN
          ii := 1;
          maxII := numColors - 1;
          cc := colorsOrder(ii);
          isColorTurn := bool(cc = color);
          WHILE ii < maxII & isColorTurn = FALSE
          DO
            ii := ii + 1;
            cc := colorsOrder(ii);
            isColorTurn := bool(cc = color)
          INVARIANT ii : 0..maxII & isColorTurn = bool(colorsOrder(ii) = color)
          VARIANT maxII - ii
          END;
          nn := ii
      END
  END;

  pickColor(cc) =
    BEGIN
      colorEnabled(cc) := TRUE
    END;


  unpickColor(cc) =
    BEGIN
      colorEnabled(cc) := FALSE
    END;


  initGame =
    BEGIN
      gameStarted := TRUE;
      VAR ii, isEnabled IN
          ii := 0;
          isEnabled := colorEnabled(colorsOrder(ii));
          WHILE isEnabled = FALSE
          DO
            ii := ii + 1;
            isEnabled := colorEnabled(colorsOrder(ii))
          INVARIANT ii : 0..(numColors - 1) & isEnabled = colorEnabled(colorsOrder(ii))
          VARIANT numColors - ii
          END;
          color := colorsOrder(ii)
      END;
      hasRoll := TRUE;
      diceValue := 0;
      sixSequenceCount := 0;
      finishList := colorsOrder;
      finishCount := 0
    END;


  // zera o valor do dado atual, e caso a peÃ§a tenha turno extra ele altera hasRoll pra TRUE
  computeAction(extraTurn, finished) =
    BEGIN
      diceValue := 0;
      IF finished = TRUE
        THEN
          finishList(finishCount) := color;
          finishCount := finishCount + 1;
          hasRoll := FALSE
      ELSIF extraTurn = TRUE
        THEN
          hasRoll := TRUE
      END
    END;


  nextTurn =
    BEGIN
      VAR numJumps, cc, isFinished, currentIndex, enabled
      IN
          currentIndex <-- turnIndex;
          numJumps := 1;
          cc := colorsOrder((currentIndex + 1) mod numColors);
          enabled := colorEnabled(cc);
          isFinished <-- isColorFinished(cc);
          WHILE numJumps < numColors & (enabled = FALSE or isFinished = TRUE)
          DO

              numJumps := numJumps + 1;
              cc := colorsOrder((currentIndex + numJumps) mod numColors);
              isFinished <-- isColorFinished(cc);
              enabled := colorEnabled(cc)

          INVARIANT 
            numJumps <= numColors &
            enabled : BOOL &
            isFinished : BOOL &
            enabled = bool(colorsOrder((currentIndex + numJumps) mod numColors) : enabledColors) &
            isFinished = bool(colorsOrder((currentIndex + numJumps) mod numColors) /: ran(finishOrder)) &
            numJumps <= min({ nn |
              nn : 1..numColors & colorsOrder((currentIndex + nn) mod numColors) : enabledColors - ran(finishOrder)
            })
          VARIANT numColors - numJumps
          END;
          hasRoll := TRUE;
          sixSequenceCount := 0;
          diceValue := 0;
          color := colorsOrder((currentIndex + numJumps) mod numColors)

      END
    END;


  value <-- rollDice =
    BEGIN
      VAR dd IN
        dd := 5;
        value := dd;
        IF dd = 6 THEN
          IF sixSequenceCount < 2 THEN
            sixSequenceCount := sixSequenceCount + 1;
            diceValue := dd
          ELSE
            sixSequenceCount := 0;
            hasRoll := FALSE;
            diceValue := 0
          END
        ELSE
          hasRoll := FALSE;
          diceValue := dd
        END
      END
    END;


  endGame =
    BEGIN
        gameStarted := FALSE
    END;

  value <-- getDiceValue =
      value := diceValue;

  cc <-- getColor =
      cc := color;

  nn <-- numWinners =
      nn := finishCount;

  cc <-- placement(nn) =
      cc := finishList(nn);
  
  bb <-- getGameStarted  = 
      bb := gameStarted;
      
  bb <-- getHasRoll =
      bb := hasRoll;

  bb <-- isColorEnabled(cc) = 
      bb:= colorEnabled(cc);
      
  nn <-- getFinishCount =
      nn := finishCount
END
